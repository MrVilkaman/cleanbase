apply plugin: 'com.android.application'
apply plugin: 'me.tatarka.retrolambda'
apply plugin: 'com.neenbedankt.android-apt'
apply from: '../artifacts.gradle'
apply from: '../dependencies.gradle'
apply plugin: 'com.github.ben-manes.versions'
android {
    // Declared in dependencies.gradle in the root of the project.
    compileSdkVersion versions.compileSdk
    buildToolsVersion versions.buildTools

    defaultConfig {
        applicationId "com.github.mrvilkaman.namegenerator"
        minSdkVersion versions.minSdk
        targetSdkVersion versions.targetSdk
        versionCode versions.code
        versionName versions.name
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        debug {
            if (project.hasProperty("mytestCoverageEnabled")) {
                testCoverageEnabled = mytestCoverageEnabled
            }
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    sourceSets {
        // Integration tests live in src/integrationTest.
        test.java.srcDir 'src/integrationTests/java'
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

configurations.all {
    resolutionStrategy {
        // Force our version of support-annotations, we have a conflict between app and androidTest dependency resolution.
        force libraries.supportAnnotations
        force libraries.supportAppCompat
    }
}

dependencies {
    compile libraries.dagger
    apt libraries.daggerCompiler
    compile libraries.javaxInject
    compile libraries.javaxAnnotationApi

    compile libraries.supportAnnotations
    compile libraries.supportAppCompat
    compile libraries.supportRecyclerView

    compile libraries.butterKnife
    apt libraries.butterKnifeCompiler

    compile libraries.retrofit2
    compile libraries.retrofit2rxjava
    compile libraries.retrofit2gson
    compile libraries.okHttp3
    // Developer tools (Developer Settings)
    compile libraries.okHttpLoggingInterceptor

    compile libraries.rxJava
    compile libraries.rxAndroid
    compile libraries.rxBindingAppcompat

    compile libraries.materialDialog
    compile libraries.materialProgress

    //tests
    testCompile libraries.junit
    testCompile libraries.supportAnnotations
    testCompile libraries.robolectric
    testCompile libraries.robolectricShadows
    testCompile libraries.assertJ
    testCompile libraries.assertJandroid
    testCompile libraries.mockitoCore
    testCompile libraries.hamcrest
    testCompile libraries.powermockXstream
    testCompile libraries.powermockApi
    testCompile libraries.powermockModulej4Rule
    testCompile(libraries.powermockModulej4) {
        exclude group: 'org.hamcrest'
    }

    androidTestCompile libraries.supportAnnotations
    androidTestCompile libraries.supportTestRunner
    androidTestCompile libraries.supportTestRules
    androidTestCompile libraries.espressoCore
    androidTestCompile(libraries.espressoContrib) {
        // Causes IncompatibleClassChangeError. As alwaysâ€¦
        exclude module: 'support-annotations'
        exclude module: 'recyclerview-v7'
        exclude module: 'support-v4'
    }
}
